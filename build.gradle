buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "2C"
group = "iguanaman"
archivesBaseName = "IguanaTweaks"

minecraft {
    version = "1.7.10-10.13.2.1291"
    runDir = "run"
    mappings = "stable_12"
    
    replaceIn "ModInfo.java"
    replace "@MOD_VERSION@", project.version
}

task buildInfo {
    def cmd = "git rev-parse HEAD"
    def proc = cmd.execute()
    proc.waitFor()
    if(proc.exitValue() == 0) {
        ext.hash = proc.text.trim()
        ext.revision = ext.hash.substring(0, 7)
    } else {
        ext.hash = ext.revision = "GITBORK"
    }
    if(project.hasProperty("buildType")) {
        if (buildType.equals("SNAPSHOT")) {
            if (System.getenv().DRONE_BUILD_NUMBER == null) {
                println "Not in drone.io. Reverting to dev buildType"
                project.version += "-DEV-" + ext.revision
            } else {
                project.version += "-snapshot-" + System.getenv().DRONE_BUILD_NUMBER
            }
        } else if (!buildType.equals("RELEASE")) {
            project.version += "-DEV-" + ext.revision
        }
    } else {
        project.version += "-DEV-" + ext.revision
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '*.info'
    }
}

jar {
    manifest {
        attributes "FMLAT": "IguanaTweaks_at.cfg",
                "Git-Hash": project.buildInfo.hash
    }
    version = "${project.minecraft.version}-${project.version}"
}
